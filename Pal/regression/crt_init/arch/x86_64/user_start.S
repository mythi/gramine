/* Our PAL loader invokes this entrypoint with all registers unspecified, except %rsp:
 *
 *   %rsp                  The stack contains the arguments and environment:
 *   0(%rsp)               argc
 *   8(%rsp)               argv[0]
 *   16(%rsp)              argv[1]
 *   ...
 *   (8*argc)(%rsp)        argv[argc] = NULL
 *   (8*(argc+1))(%rsp)    envp[0]
 *   (8*(argc+2))(%rsp)    envp[1]
 *   ...
 *   (8*(argc+n))(%rsp)    envp[n] = NULL
 *   (8*(argc+n+1))(%rsp)  auxv[0] = AT_NULL
 *
 * For details on the PAL loader, see Pal/src/db_rtld.c: start_execution()
 */

	.text
	.globl _start
	.type _start,@function
_start:
    .cfi_startproc

    # Clear the frame pointer. The ABI suggests this be done, to mark
    # the outermost frame obviously.
    xorq %rbp, %rbp

    # Arguments for PAL-executable main:
    movq 0(%rsp), %rdi  # argc
    leaq 8(%rsp), %rsi  # args

    # Required by System V AMD64 ABI.
    andq  $~0xF, %rsp

    callq *main@GOTPCREL(%rip)

	mov %rax, %rdi
    callq *DkProcessExit@GOTPCREL(%rip)

    .cfi_endproc
